apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply from: "$rootDir/buildscripts/ktlint.gradle"
apply from: "$rootDir/buildscripts/testlog.gradle"
apply from: "$rootDir/buildscripts/detekt.gradle"
apply from: "$rootDir/buildscripts/jacoco.gradle"
apply from: "$rootDir/buildscripts/maven-check.gradle"
apply from: "$rootDir/buildscripts/git-hash.gradle"

dependencies {

    // QMobile libraries
    if (System.getenv('CI_DEPS_TO_BE_FETCHED')) {
        implementation(QMobileAPI) { changing = true }
        implementation(QMobileDataStore) { changing = true }
    } else {
        implementation project(':qmobileapi')
        implementation project(':qmobiledatastore')
    }

    // Common
    implementation Libs.androidx_appcompat
    implementation Libs.kotlin_coroutines_android
    implementation Libs.kotlin_coroutines_core
    implementation Libs.kotlin_reflect
    implementation Libs.kotlin_stdlib

    // Lifecycle
    implementation Libs.lifecycle_livedata
    implementation Libs.lifecycle_runtime_ktx
    implementation Libs.lifecycle_viewmodel

    // Jackson
    implementation Libs.jackson

    // Room
    implementation Libs.androidx_room
    implementation Libs.androidx_room_runtime
    kapt Libs.androidx_room_compiler
    implementation Libs.sqlite
    implementation Libs.room_paging

    // Paging
    implementation Libs.androidx_paging3

    // Utils
    implementation Libs.multidex
    implementation Libs.okhttp
    implementation Libs.retrofit_converter_gson
    implementation Libs.rxjava
    implementation Libs.timber

    // Testing
    testImplementation Libs.androidx_core_testing
    testImplementation Libs.androidx_junit
    testImplementation Libs.junit
    testImplementation Libs.kotlin_coroutines_test
    testImplementation Libs.lifecycle_testing
    testImplementation Libs.mockito
    testImplementation Libs.robolectric
}

def libraryGroupId = 'com.qmobile.qmobiledatasync'
def libraryArtifactId = 'qmobiledatasync'
def libraryVersion = Versions.qmobiledatasync
def buildVariant = 'release'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion + '-' + currentGitBranch()

            artifactId libraryArtifactId
            artifact("$buildDir/outputs/aar/${artifactId}-${buildVariant}.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("${buildVariant}CompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}

artifactory {
    def machineIP = System.getenv('ARTIFACTORY_MACHINE_IP')
    contextUrl = "http://${machineIP}:8081/artifactory"
    publish {
        repository {
            repoKey = "libs-release-local"
            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
        }
    }
}

task generateMySources(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
task addMySourcesToAar(type: Jar) {
    archiveFileName.set("${libraryArtifactId}WithSources.aar")
    destinationDirectory.set(file("build"))
    from zipTree("build/outputs/aar/${libraryArtifactId}-${buildVariant}.aar")
    from fileTree("build").include("libs/${libraryArtifactId}-sources.jar")
}
afterEvaluate { project ->
    project.tasks.preBuild.dependsOn generateMySources
    project.addMySourcesToAar.dependsOn build
}
artifacts {
    archives addMySourcesToAar.archivePath
}

artifactoryPublish {
    doFirst {
        uploadGitHash(libraryArtifactId)
    }
}